plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'java'
    id 'maven-publish'
}

group = "com.github.chuxubank"
version = "1.3"

repositories {
    mavenCentral()
}

dependencies {
    implementation "com.ceridwen.circulation:ceridwen-standard-interchange-protocol-library:2.9.7"
    implementation "me.champeau.openbeans:openbeans:1.0.2"
}

shadowJar {
    configurations = [project.configurations.compileClasspath]
    dependencies {
        exclude(dependency("junit:junit"))
        exclude(dependency("org.hamcrest:hamcrest-core"))
        exclude(dependency("commons-net:commons-net"))
        exclude(dependency {
            it.moduleGroup == 'io.netty'
        })
    }
    relocate "java.beans", "com.googlecode.openbeans"
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
        }
    }
}
